// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // External Load Multiplexor
    Mux16(a=incout, b=in, sel=load, out=loadout);
    
    // Reset Multiplexor
    Mux16(a=loadout, sel=reset, out=regin);

    // Incrementor Multiplexor
    Mux16(a=regout, b=addout, sel=inc, out=incout);

    // 3-Way Or for register load flag
    Or(a=reset, b=inc, out=flags);
    Or(a=load, b=flags, out=rloadin);

    // Register
    Register(in=regin, load=rloadin, out=out, out=regout);

    // Adder
    Inc16(in=regout, out=addout);
    }
